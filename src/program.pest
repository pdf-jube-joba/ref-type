new_definition = {
    "Def" ~
    variable ~ var_annot_paren* ~ ":" ~ expression ~ ":=" ~ expression
}

new_assumption = {
    "Assum" ~
    variable ~ ":" ~ expression 
}

new_inductive = {
    "Inductive" ~
    name ~ ":" ~ arity ~ "with" ~ constructor_definition*
}

arity = {
    ( var_annot_paren ~ "->" )* ~ sort
}

constructor_terminate = {
    name ~ expression*
}

positive = {
    "[" ~ ( var_annot_paren ~ "->" )* ~ constructor_terminate ~ "]"
}

constructor_definition = {
   "|" ~ name ~ ":" ~ ((var_annot_paren | expression | positive) ~ "->")* ~ constructor_terminate
}

command_parse = {
    "Parse" ~
    expression
}

command_subst = {
    "Subst" ~
    expression ~ "[" ~ variable ~ ":=" ~ expression ~ "]"
}

command_alpha_eq = {
    "AlphaEq" ~
    expression ~ "=" ~ expression
}

command_top_reduction = {
    "TopReduce" ~
    expression
}

command_reduction = {
    "Reduce" ~ 
    expression
}

command_normalize = {
    "Normalize" ~ expression
}

command_beta_equiv = {
    "BetaEq" ~ expression ~ "=" ~ expression
}

command_check = {
    "Check" ~ expression ~ ":" ~ expression
}

command_infer = {
    "Infer" ~ expression
}

COMMAND_TERMINATE = _{ ";" }

command = { (
    command_parse
    | command_check
    | command_infer
    | command_subst
    | command_alpha_eq
    | command_top_reduction
    | command_reduction
    | command_normalize
    | command_beta_equiv
    | new_definition
    | new_assumption
    | new_inductive
) ~ COMMAND_TERMINATE }
