WHITESPACE = _{ " " | "\n" }

sort_set = { "SET" }
sort_prop = { "PROP" }
sort_type = { "TYPE" }
sort = { sort_set | sort_prop | sort_type }

// variable は小文字始まり
identifier = @{ ('a'..'z') ~ ('a'..'z' | 'A'..'Z' | '0'..'9')* }
unused_identifier = @{"_"}

// inductive_type と constructor の定義
type_name = @{ ('A'..'Z') ~ ('a'..'z' | 'A'..'Z' | '0'..'9')* }
constructor_name = @{ ('A'..'Z') ~ ('a'..'z' | 'A'..'Z' | '0'..'9')* }

var_annot = { identifier ~ ":" ~ expression }

dependent_prod_form = {
    "(" ~ var_annot ~ ")"
    ~ "->" ~ expression
}

dependent_prod_intro = {
    "\\"  ~ var_annot ~ "." ~ expression
}

dependent_prod_elim = {
    "(" ~ expression ~ expression ~ ")"
}

inductive_type = {
    type_name ~ expression*
}

inductive_constructor = {
    type_name ~ "::" ~ constructor_name ~ expression*
}

inductive_eliminator = {
    "elim" ~ expression ~ "return" ~ expression ~ "with" ~ expression*
}

subset_type = {
    "{" ~ var_annot ~ "|" ~ expression ~ "}"
}

power_type = {
    "Pow" ~ expression
}

take_exp = {
    "take" ~ var_annot ~ "." ~ expression
}

rec_exp = {
    "rec" ~ var_annot ~ var_annot ~ "="
}

expression = {
    | sort
    | dependent_prod_form
    | dependent_prod_intro
    | dependent_prod_elim
    | identifier
    | inductive_type
    | inductive_constructor
    | inductive_eliminator
    | expression_readable
}

expression_readable = { "(" ~ expression ~ ")" }

COMMAND_TERMINATE = _{ ";" }

new_definition = { "def" ~ identifier ~ ":=" ~ expression ~ COMMAND_TERMINATE }
new_assumption = { "assum" ~ identifier ~ ":" ~ expression ~ COMMAND_TERMINATE }

new_inductive = {
    "inductive" ~ type_name ~ "(" ~ identifier ~ ")" ~ arity ~ "with" ~ constructor_definition* ~ COMMAND_TERMINATE
}

arity = {
    ( "(" ~ var_annot ~ ")" ~ "->" )* ~ sort
}

constructor_terminate = {
    identifier ~ expression*
}

positive = {
    ( "(" ~ var_annot ~ ")" ~ "->" )*
    ~ constructor_terminate
}

constructor_non_occur = {
    "(" ~ var_annot ~ ")" ~ "->" ~ constructor_rec
}

constructor_positive_occur = {
    positive ~ "->" ~ constructor_rec
}

constructor_rec = {
    | constructor_terminate
    | constructor_non_occur
    | constructor_positive_occur
}

constructor_definition = {
    constructor_name ~ ":" ~ constructor_rec
}

command_parse = { "Parse" ~ expression ~ COMMAND_TERMINATE }
command_check = { "Check" ~ expression ~ ":" ~ expression ~ COMMAND_TERMINATE}
command_infer = { "Infer" ~ expression ~ COMMAND_TERMINATE }
command_alpha_eq = { "Alpha_eq" ~ expression ~ "=" ~ expression ~ COMMAND_TERMINATE }
command_top_reduction = { "TopReduce" ~ expression ~ COMMAND_TERMINATE }
command_reduction = { "Reduce" ~ expression ~ COMMAND_TERMINATE }
command_normalize = { "Normalize" ~ expression ~ COMMAND_TERMINATE }
command_beta_equiv = { "BetaEq" ~ expression ~ "=" ~ expression ~ COMMAND_TERMINATE }

command = {
    | command_parse
    | command_check
    | command_alpha_eq
    | command_top_reduction
    | command_reduction
    | command_normalize
    | new_definition
    | new_assumption
    | new_inductive
}
