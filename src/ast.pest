WHITESPACE = _{ " " | "\n" }
COMMENT = _{ "/*" ~ (!"*/" ~ ANY)* ~ "*/" }

sort_set = { "SET" }
sort_prop = { "PROP" }
sort_type = { "TYPE" }
sort = { sort_set | sort_prop | sort_type }

// variable は小文字始まり
variable = @{ ('a'..'z') ~ ('a'..'z' | 'A'..'Z' | '0'..'9')* }
unused_variable = @{"_"}

// x: T のこと
var_annot = { (variable | unused_variable) ~ ":" ~ expression }

dependent_prod_form = {
    "(" ~ var_annot ~ ")"
    ~ "->" ~ expression
}

dependent_prod_intro = {
    "\\"  ~ var_annot ~ "." ~ expression
}

dependent_prod_elim = {
    "(" ~ expression ~ expression* ~ ")"
}

// inductive_type と constructor の定義
type_name = @{ ('A'..'Z') ~ ('a'..'z' | 'A'..'Z' | '0'..'9')* }
constructor_name = @{ ('A'..'Z') ~ ('a'..'z' | 'A'..'Z' | '0'..'9')* }

inductive_type = {
    type_name
}

inductive_constructor = ${
    type_name ~ "::" ~ constructor_name
}

inductive_eliminator = {
    "elim" ~ "(" ~ type_name ~ ")" ~ expression ~ "return" ~ expression ~ "with" ~ expression*
}

subset_type = {
    "{" ~ var_annot ~ "|" ~ expression ~ "}"
}

power_type = {
    "Pow" ~ expression
}

predicate = {
    "Pred" ~ expression ~ expression
}

take_exp = {
    "take" ~ var_annot ~ "." ~ expression
}

rec_exp = {
    "rec" ~ var_annot ~ var_annot ~ "="
}

expression = {
    | sort
    | dependent_prod_form
    | dependent_prod_intro
    | dependent_prod_elim
    | inductive_eliminator
    | inductive_constructor
    | inductive_type
    | variable
    | expression_readable
}

test = {
    | inductive_type
    | inductive_constructor
    | test_readable
}

test_readable = { "(" ~ test ~ ")"}

expression_readable = { "(" ~ expression ~ ")" }

COMMAND_TERMINATE = _{ ";" }

new_definition = { "Def" ~ variable ~ ":=" ~ expression ~ COMMAND_TERMINATE }
new_assumption = { "Assume" ~ variable ~ ":" ~ expression ~ COMMAND_TERMINATE }

new_inductive = {
    "Inductive" ~ type_name ~ ":" ~ arity ~ "with" ~ constructor_definition* ~ COMMAND_TERMINATE
}

// inductive definitions
arity = {
    ( "(" ~ var_annot ~ ")" ~ "->" )* ~ sort
}

constructor_terminate = {
    type_name ~ expression*
}

positive = {
    "(" ~ 
        ( "(" ~ var_annot ~ ")" ~ "->" )*
        ~ constructor_terminate
    ~ ")"
}

constructor_non_occur = {
    "(" ~ var_annot ~ ")" ~ "->" ~ constructor_rec
}

constructor_positive_occur = {
    positive ~ "->" ~ constructor_rec
}

constructor_rec = {
    | constructor_terminate
    | constructor_positive_occur
    | constructor_non_occur
}

constructor_definition = {
    "|" ~ constructor_name ~ ":" ~ constructor_rec
}

// check command 
command_parse = { "Parse" ~ expression ~ COMMAND_TERMINATE }

command_subst = { "Subst" ~ expression ~ "[" ~ variable ~ ":=" ~ expression ~ "]" ~ COMMAND_TERMINATE }
command_alpha_eq = { "AlphaEq" ~ expression ~ "=" ~ expression ~ COMMAND_TERMINATE }
command_top_reduction = { "TopReduce" ~ expression ~ COMMAND_TERMINATE }
command_reduction = { "Reduce" ~ expression ~ COMMAND_TERMINATE }
command_normalize = { "Normalize" ~ expression ~ COMMAND_TERMINATE }
command_beta_equiv = { "BetaEq" ~ expression ~ "=" ~ expression ~ COMMAND_TERMINATE }

command_check = { "Check" ~ expression ~ ":" ~ expression ~ COMMAND_TERMINATE}
command_infer = { "Infer" ~ expression ~ COMMAND_TERMINATE }

command = {
    | command_parse
    | command_check
    | command_subst
    | command_alpha_eq
    | command_top_reduction
    | command_reduction
    | command_normalize
    | new_definition
    | new_assumption
    | new_inductive
}
