WHITESPACE = _{ " " | "\n" }
COMMENT = _{ "/*" ~ (!"*/" ~ COMMENT* ~ ANY)* ~ "*/" }

number = @{ ('0' .. '9')+ }

sort_set = { "SET" }
sort_prop = { "PROP" }
sort_type = { "TYPE" }
sort_univ = ${ "UNIV" ~ "(" ~ number ~ ")" }
sort = { sort_set | sort_prop | sort_type | sort_univ }

keyword = {sort | "elim" | "return" | "with" | "end" |
    "Proof" | "Pow" | "Pred" | "Identity" | "Refl" | "Idind" | "Exists" | "Take" 
}

variable = @{ &!keyword ~ ('a'..'z') ~ ('a'..'z' | 'A'..'Z' | '0'..'9' | "_")* }
unused_variable = @{"_"}
name = @{ &!keyword ~ ('A'..'Z') ~ ('a'..'z' | 'A'..'Z' | '0'..'9')* }

expression = { pre_arrows* ~ smalls}
pre_arrows = { (smalls | var_annot_paren) ~ delimitor }
smalls = { small+ }
delimitor = { prod | lamd }
prod = { "->" }
lamd = { "|->" }

small = {
	 sort | app | proof | subset | powerset | predicate
    | ind_constructor | ind_name | ind_elim
    | identity | identity_refl 
    | exists | take_operator
    | variable
}

var_annot = { (variable | unused_variable) ~ ":" ~ expression }
var_annot_paren = _{ "(" ~ var_annot ~ ")" }

app = { "(" ~ expression+ ~ ")" }

ind_name = { name }
ind_constructor = { name ~ "::" ~ name }
ind_elim = {
    "elim" ~ "(" ~ name ~ ")" ~ expression ~ "return" ~ expression ~ "with" ~
    ("|" ~ name ~ "=>" ~ expression)* ~ "end"
}

proof = { "Proof" ~ "(" ~ expression ~ ")" }
subset = { "{" ~ var_annot ~ "|" ~ expression ~ "}" }
powerset = { "Power" ~ "(" ~ expression ~ ")" }
predicate = { "Pred" ~ "(" ~ expression ~ "," ~ expression ~ ")" }

identity = { "Id" ~ "(" ~ expression  ~ "," ~ expression ~ "," ~ expression ~ ")" }
identity_refl = { "Refl" ~ "(" ~ expression ~ "," ~ expression ~ ")" }

exists = { "Exists" ~ expression }
take_operator =  { "Take" ~ var_annot_paren ~ "|->" ~ expression }
